#!/usr/bin/env coffee

{ log } = require 'lightsaber'
fs = require 'fs'
bitcore = require 'bitcore'
Insight = require('bitcore-explorers').Insight
ClaimData = require './src/ClaimData.coffee'


class ClaimCert
  constructor: (dataFile)->
    @data = ClaimData.load(dataFile)
    @insight = new Insight 'https://test-insight.bitpay.com', @data.network()

  makeClaim: ->
    @insight.getUnspentUtxos @data.tempAddress(), (err, utxos)=>
      if utxos.length == 0
        log "No unspent transactions. You may need to fund this address: #{@data.tempAddress()}"
      else
        total = utxos[0].satoshis
        fee = 10000

        # TODO: OP_RETURN
        txn = new bitcore.Transaction()
          .from utxos[0]
          .to(@data.destinationAddress(), (total-fee))
          .sign(@data.tempPK)

        log "Broadcasting transaction: #{txn.serialize()}"
        @insight.broadcast txn, (err, txnid)->
          if err
            console.error err
          else
            log "Transaction ID: #{txnid}"


domainName = "www.mydomain.com"
claim = new ClaimCert("tmp/certs/#{domainName}.json")

claim.makeClaim()
