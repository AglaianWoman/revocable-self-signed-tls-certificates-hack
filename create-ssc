#!/usr/bin/env coffee

{ log } = require 'lightsaber'
bitcore = require 'bitcore'
require 'shelljs/global'
forge = require 'node-forge'

digest = (text, options) ->
  options.encoding ?= 'utf-8'
  message_digest = forge.md[options.algorithm].create()
  message_digest.update text, options.encoding
  {
    message_digest
    hex: message_digest.digest().toHex()
  }

#################################
# main
#################################

if not which 'openssl'
  echo 'Sorry, this script requires openssl'
  exit 1

#################################
# create tmp directory to store
#################################

domainName =  "www.mydomain.com"
mkdir '-p', 'tmp/certs'
cd 'tmp/certs'

#################################
# bitcoin address
#################################

privateKey = new bitcore.PrivateKey null, bitcore.Networks.testnet
btcAddress = privateKey.toAddress()
privateKeyWIF = privateKey.toWIF()

#################################
# save wif
#################################

exec "echo '#{privateKeyWIF}' > #{domainName}.wif"
exec "echo '#{btcAddress}' > #{domainName}.btc"


#################################
# generate cert
#################################

certSubject = "/C=NO/ST=None/L=Aethers/O=OrgName/OU=#{btcAddress}/CN=#{domainName}"

exec """
  openssl req \
    -new \
    -newkey rsa:2048 \
    -nodes \
    -x509 \
    -days 365 \
    -subj "#{certSubject}" \
    -keyout #{domainName}.key \
    -out #{domainName}.cert
"""
#################################
# getSignatureFromCert
#################################

cert = "#{domainName}.cert"
certText = exec "openssl x509 -noout -text -in #{cert}", silent: true

match = certText.output.match /\n\s+Signature Algorithm: sha1WithRSAEncryption\s*((?:[0-9a-f]{2}[:\s]+)+)/
block = match[1]
signature = block.replace /\W/g, ''
buf = new Buffer signature, 'hex'
signatureDigest = digest buf, algorithm: 'sha256', encoding: 'HEX'
signatureSha256 = signatureDigest.hex

#################################
# save hashed signature
#################################

exec "echo '#{signatureSha256}' > #{domainName}.sighash"

log ""
log "Certificate Validity Bitcoin Address: " + btcAddress
log "Certificate Validity WIF Private: " + privateKeyWIF
log ""
log "Certificate Signature (sha1WithRSAEncryption): " + signature
log "Hash of the Certificate Signature (sha256): " + signatureSha256
log ""
log "Please put 0.005 BTC to #{btcAddress} to maintain this claim. (TestNet only)"
